plugins {
    id 'application'
    id 'bisq.post-build'
    alias(libs.plugins.shadow)
    alias(libs.plugins.springDependencyManagement)
    alias(libs.plugins.springframeworkBoot)
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group 'bisq'
version = file("src/main/resources/version.txt").text.trim()

application {
    project.mainClassName = 'bisq.relay.Main'
}

bootJar {
    project.mainClassName = 'bisq.relay.Main'
}

[test, run, bootRun].each { runTask ->
    configure(runTask) {
        systemProperties System.properties
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version
)

configurations.configureEach {
    exclude group: 'com.github.bisq-network:bitcoinj'
    exclude group: 'org.fxmisc.easybind'
}

dependencies {
    implementation 'bisq:common'
    implementation 'bisq:proto'

    implementation libs.spring.boot.dependencies
    implementation libs.spring.boot.web
    implementation libs.spring.boot.validation

    implementation libs.codec
    implementation libs.firebase
    implementation libs.pushy
    implementation libs.spark.core

    implementation libs.logback.core
    implementation libs.logback.classic
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    implementation libs.slf4j.api

    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testImplementation libs.junit.jupiter
    testImplementation libs.spring.boot.test
}

clean.doFirst {
    // Remove the project root level 'bisq-*' scripts and 'lib' dir generated at the end of the assemble task.
    delete fileTree(dir: projectDir, include: ['bisq-relay', 'bisq-relay.bat']), 'lib'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
