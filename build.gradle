plugins {
    id 'application'
    id 'bisq.post-build'
    alias(libs.plugins.shadow)
    alias(libs.plugins.springDependencyManagement)
    alias(libs.plugins.springframeworkBoot)
    alias(libs.plugins.gradleGitProperties)
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group 'bisq'
version = file("src/main/resources/version.txt").text.trim()

application {
    project.mainClassName = 'bisq.relay.Main'
}

bootJar {
    project.mainClassName = 'bisq.relay.Main'
}

[test, run, bootRun].each { runTask ->
    configure(runTask) {
        systemProperties System.properties
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

jar.manifest.attributes(
        "Implementation-Title": project.name,
        "Implementation-Version": version
)

dependencies {
    implementation libs.spring.boot.dependencies
    implementation libs.spring.boot.web
    implementation libs.spring.boot.validation

    implementation(libs.firebase) {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation libs.pushy

    implementation libs.spring.boot.actuator
    runtimeOnly libs.micrometer.registry.prometheus

    implementation libs.apache.commons.lang3

    implementation libs.slf4j.api
    implementation libs.logback.core
    implementation libs.logback.classic

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok

    testImplementation libs.junit.jupiter
    testImplementation libs.spring.boot.test
}

/**
 * Generate META-INF/build-info.properties at build time so /actuator/info shows build info.
 */
springBoot {
    buildInfo {
        properties {
            name = project.name
            version = project.version.toString()
        }
    }
}

/**
 * Configure git-properties so /actuator/info shows git info.
 */
gitProperties {
    keys = ['git.branch', 'git.commit.time', 'git.commit.id']
    failOnNoGitDirectory = false
}

clean.doFirst {
    // Remove the project root level 'bisq-*' scripts and 'lib' dir generated at the end of the assemble task.
    delete fileTree(dir: projectDir, include: ['bisq-relay', 'bisq-relay.bat']), 'lib'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
